managed implementation in class zbp_inct_r_jrz unique;
strict ( 2 );
with draft;

define behavior for ZINCT_R_JRZ alias Incident
persistent table zdt_inct_jrz
draft table zdt_d_inct_jrz
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( instance )
{
  create;
  update;
  delete;
  field ( numbering : managed, readonly ) IncUuid;
  field ( readonly ) IncidentId,

  Creationdate,
  Changedate,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy;

  //  field( features : instance) status;
  field ( mandatory ) Title, Description, Status, Priority;

  action ( features : instance, authorization : update ) acceptIncident result [1] $self;
  action ( features : instance, authorization : update ) rejectIncident result [1] $self;
  action ( features : instance, authorization : update ) changeStatus
    parameter ZINCT_AE_JRZ result [1] $self;

  internal action change_status_priority;
  validation validate_Status on save { create; field Status; }
  validation validate_Priority on save { create; field Priority; }

  determination setIncNumber on save { create; }
  determination setCreationDate on save { create; }
  determination setChangeDate on modify { create; }

  determine action validate_StatusId { validation validate_Status; }
  determine action validate_PriorityId { validation validate_Priority; }

  side effects
  {
    determine action validate_StatusId executed on field Status affects messages;
    determine action validate_PriorityId executed on field Priority affects messages;
  }

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare
  {
    validation validate_Status;
    validation validate_Priority;
  }

  mapping for zdt_inct_jrz
    {
      IncUuid            = inc_uuid;
      IncidentId         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      Creationdate       = creation_date;
      Changedate         = change_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }


}